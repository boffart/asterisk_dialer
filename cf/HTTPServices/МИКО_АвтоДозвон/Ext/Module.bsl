
// Точка входа запров клиента.
// 
Функция ОбработкаЗапроса(Запрос)
	Результат = "";
	// http://192.168.1.76/unf_test/hs/rout/owner_number?callerid=74952293014&
	// testrout:testrout
	event	 = Запрос.ПараметрыЗапроса.Получить("event");
	Если "putresult" = event Тогда
		putresult(Запрос);
	ИначеЕсли "getpart" = event Тогда	
		Результат = getpart();
	КонецЕсли; 
		
	Ответ = Новый HTTPСервисОтвет(200);
	Ответ.УстановитьТелоИзСтроки(Результат);
	Возврат Ответ;
КонецФункции

// Получаем новую порцию номеров телефонов.
// 
Функция getpart()
	
	// http://172.16.32.164/unf/hs/database/task?event=getpart&
	врЗапрос = Новый Запрос;             
	врЗапрос.Текст = 
		"ВЫБРАТЬ
		|	МИКО_обКонтактыКОбзвону.НомерТелефона,
		|	МИКО_обКонтактыКОбзвону.Статус,
		|	МИКО_обКонтактыКОбзвону.Попытки,
		|	МИКО_обКонтактыКОбзвону.ТекстСообщения  
		|ИЗ
		|	РегистрСведений.МИКО_обКонтактыКОбзвону КАК МИКО_обКонтактыКОбзвону
		|ГДЕ
		|	МИКО_обКонтактыКОбзвону.Статус = ЗНАЧЕНИЕ(Перечисление.МИКО_обСтатусОбработкиВызова.ВОчереди)";
	
	РезультатЗапроса = врЗапрос.Выполнить().Выгрузить();
	Результат = "";
	                                                    
	НачатьТранзакцию();
	Для каждого Контакт Из РезультатЗапроса Цикл
		// Готовим задачу в формете JSON.
		Если ЗначениеЗаполнено(Результат) Тогда
			Результат = Результат + ",";
		КонецЕсли; 
		ТекстСообщения = КодироватьСтроку(Контакт.ТекстСообщения, СпособКодированияСтроки.КодировкаURL);
		Результат = Результат + "{""number"": """+Контакт.НомерТелефона+""", ""text"": """+ТекстСообщения+"""}";	        // 
		
		// Меняем статус. 
		МЗ = РегистрыСведений.МИКО_обКонтактыКОбзвону.СоздатьМенеджерЗаписи();
		ЗаполнитьЗначенияСвойств(МЗ, Контакт);
		МЗ.Статус = Перечисления.МИКО_обСтатусОбработкиВызова.ВОбработке;
		МЗ.Записать();
	КонецЦикла;
	ЗафиксироватьТранзакцию();
	
	Результат = "[" + Результат + "]";
	Возврат Результат;

КонецФункции // getpart()

// Сохранение результата телефонного звонка.
// 
Процедура putresult(Запрос)

	DIALERSTATUS	= Запрос.ПараметрыЗапроса.Получить("DIALERSTATUS");
	CID	 			= Запрос.ПараметрыЗапроса.Получить("CID");
	IDCALL 		 	= Запрос.ПараметрыЗапроса.Получить("ID");
	TIME	 		= Запрос.ПараметрыЗапроса.Получить("TIME");
	
	МЗСтатус = РегистрыСведений.МИКО_обКонтактыКОбзвону.СоздатьМенеджерЗаписи();
	МЗСтатус.НомерТелефона = CID;
	МЗСтатус.Прочитать();
	Если "fail_generate_speach" = DIALERSTATUS Тогда
		// Вернем в очередь. Ошлибка генерации речи.
		Статус = Перечисления.МИКО_обСтатусОбработкиВызова.ВОчереди;	
	ИначеЕсли "START" = DIALERSTATUS Тогда 
		Статус = Перечисления.МИКО_обСтатусОбработкиВызова.ВОбработке;
	ИначеЕсли "ANSWERED" = DIALERSTATUS Тогда 
		// Обновим статус.
		Статус = Перечисления.МИКО_обСтатусОбработкиВызова.Разговаривает;
	ИначеЕсли "END" = Лев(DIALERSTATUS, 3)
		 	  И МЗСтатус.Статус = Перечисления.МИКО_обСтатусОбработкиВызова.Разговаривает Тогда	
		// Обработка завершена. Дозвонились клиенту.
		Статус = Перечисления.МИКО_обСтатусОбработкиВызова.Завершен;
	ИначеЕсли "END" = Лев(DIALERSTATUS, 3) Тогда	
		// Вернем в очередь.
		Статус = Перечисления.МИКО_обСтатусОбработкиВызова.ВОчереди;
	КонецЕсли;                     
	// Вернем в очередь.
	МЗСтатус.Попытки = МЗСтатус.Попытки + 1;
	МЗСтатус.Статус = Статус;
	МЗСтатус.Записать();

	//
	МЗ = РегистрыСведений.МИКО_обСобытияОбзвона.СоздатьМенеджерЗаписи();
	МЗ.status = DIALERSTATUS;
	МЗ.id = IDCALL;
	МЗ.НомерТелефона = CID; 
	МЗ.data = "DIALERSTATUS="+DIALERSTATUS + ". ID="+IDCALL + ". TIME=" + TIME + ". CID="+CID;
	МЗ.Записать();

КонецПроцедуры
 
